#pragma kernel PreCalculations // 0
#pragma kernel CalculateDensities // 1
#pragma kernel PrepSpringData // 2
#pragma kernel TransferAllSpringData // 3
#pragma kernel ParticleForces // 4
#pragma kernel UpdatePositions // 5

#include "SimulationResources.hlsl"
#include "CompositionResources.hlsl"

// Constants
const int4 ChunksNum;
const int ParticlesNum;
const int Width;
const int Height;
const int Depth;
const int ParticleSpringsCombinedHalfLength;
const int MaxInfluenceRadius;
const int MaxInfluenceRadiusSqr;
const float InvMaxInfluenceRadius;

const float LookAheadFactor;
const float StateThresholdPadding;
const float BorderPadding;
const float MaxInteractionRadius;

const float InteractionAttractionPower;
const float InteractionFountainPower;
const float InteractionTemperaturePower;

// Per-timestep-set constants
const float DeltaTime;
const bool TransferSpringData;

const float MouseX;
const float MouseY;
const bool RMousePressed;
const bool LMousePressed;

// Particles - Buffers
RWStructuredBuffer<int2> SpatialLookup; // [ParticlesNum](particleIndex, ChunkKey)
RWStructuredBuffer<int> StartIndices;

RWStructuredBuffer<SpringStruct> ParticleSpringsCombined; // Buffer_A / Buffer_B

const bool FrameBufferCycle;
RWStructuredBuffer<int> ChunkSizes;
RWStructuredBuffer<int2> SpringCapacities; // [](baseChunkCapacity, neighboorChunksCapacity)
RWStructuredBuffer<int> SpringStartIndices_dbA; // Result A
RWStructuredBuffer<int> SpringStartIndices_dbB; // Result B

RWStructuredBuffer<PTypeStruct> PTypes;
RWStructuredBuffer<PDataStruct> PData;

bool ValidChunk(int ChunkX, int ChunkY, int ChunkZ)
{
    return ChunkX >= 0 && ChunkX < ChunksNum.x && ChunkY >= 0 && ChunkY < ChunksNum.y && ChunkZ >= 0 && ChunkZ < ChunksNum.z;
}

int GetChunkKey(int chunkX, int chunkY, int chunkZ)
{
    return chunkX + chunkY * ChunksNum.x + chunkZ * ChunksNum.w;
}

float2 CalcDensity(int pIndex) // (density, nearDensity)
{
    PDataStruct PData_i = PData[pIndex];
    float InfluenceRadius_i = PTypes[Extract_PType(PData_i.LastChunkKey_PType_POrder)].InfluenceRadius;

    if (TransferSpringData)
    {
        int newChunkX = (int)(PData_i.Position.x * InvMaxInfluenceRadius);
        int newChunkY = (int)(PData_i.Position.y * InvMaxInfluenceRadius);
        int newChunkZ = (int)(PData_i.Position.z * InvMaxInfluenceRadius);
        int newChunkKey = GetChunkKey(newChunkX, newChunkY, newChunkZ);
        Set_LastChunkKey(PData_i.LastChunkKey_PType_POrder, newChunkKey);
        PData[pIndex].LastChunkKey_PType_POrder = PData_i.LastChunkKey_PType_POrder;
    }

    int lastChunkKey = Extract_LastChunkKey(PData_i.LastChunkKey_PType_POrder);

    int chunkX = (uint)lastChunkKey % ChunksNum.x;
    int chunkY = (uint)((uint)lastChunkKey / ChunksNum.x) % ChunksNum.y;
    int chunkZ = (int)((uint)lastChunkKey / ChunksNum.w);

    float totDensity = 0.0;
    float totNearDensity = 0.0;
    for (int x = -1; x <= 1; x++)
    {
        int curChunkX = chunkX + x;

        for (int y = -1; y <= 1; y++)
        {
            int curChunkY = chunkY + y;

            for (int z = -1; z <= 1; z++)
            {
                int curChunkZ = chunkZ + z;

                if (!ValidChunk(curChunkX, curChunkY, curChunkZ)) {continue;}

                int chunkKey = GetChunkKey(curChunkX, curChunkY, curChunkZ);
                int startIndex = StartIndices[chunkKey];

                int Index = startIndex;
                while (Index < ParticlesNum && chunkKey == SpatialLookup[Index].y)
                {
                    int otherPIndex = SpatialLookup[Index].x;
                    PTypeStruct PType_otherPIndex = PTypes[Extract_PType(PData[otherPIndex].LastChunkKey_PType_POrder)];


                    // -- Spring handling --

                    if (TransferSpringData)
                    {
                        if (x == 0 && y == 0)
                        {
                            if (pIndex == otherPIndex)
                            {
                                int pOrder = Index - startIndex;
                                Set_POrder(PData_i.LastChunkKey_PType_POrder, pOrder);
                                PData[pIndex].LastChunkKey_PType_POrder = PData_i.LastChunkKey_PType_POrder;
                            }
                        }
                    }

                    float avgInfluenceRadius = avg(InfluenceRadius_i, PType_otherPIndex.InfluenceRadius);

                    // -- Calculate densities --

                    float dst = length(PData_i.PredPosition - PData[otherPIndex].PredPosition);
                    totDensity += PType_otherPIndex.Mass * SmoothLiquid_optimised(dst, avgInfluenceRadius);
                    totNearDensity += PType_otherPIndex.Mass * SmoothLiquidNear_optimised(dst, avgInfluenceRadius);

                    // -- Increment Index each iteration - Chunk particle search algorithm --

                    Index += 1;
                }
            }
        }
    }

    // // density ∝ temperature
    // float tempCoefficient = 0.0033 * PData_i.Temperature;

    return float2(totDensity, totNearDensity);
}

// Only handles one component
float2 BoundraryCheck(float pos, float vel, float Damping, float max)
{
    float velFactor = 1 - Damping;

    if (pos > max - BorderPadding)
    {
        vel = -abs(vel) * velFactor;
        pos = max - BorderPadding;
    } else if (pos < BorderPadding) {
        vel = abs(vel) * velFactor;
        pos = BorderPadding;
    }
    
    return float2(pos, vel);
}

float DensityToPressure(float density, float targetDensity, float pressure)
{
    float densityError = density - targetDensity;
    float Pressure = densityError * pressure;
    return Pressure;
}

float SharedPressure(float densityA, float densityB, float targetDensityA, float targetDensityB, float pressureA, float pressureB)
{
    float PressureA = DensityToPressure(densityA, targetDensityA, pressureA);
    float PressureB = DensityToPressure(densityB, targetDensityB, pressureB);
    float sharedPressure = avg(PressureA, PressureB);

    return sharedPressure;
}

float SharedNearPressure(float nearDensityA, float nearDensityB, float nearPressureMultiplierA, float nearPressureMultiplierB)
{
    float nearPressureA = nearDensityA * nearPressureMultiplierA;
    float nearPressureB = nearDensityB * nearPressureMultiplierB;
    float sharedNearPressure = avg(nearPressureA, nearPressureB);

    return sharedNearPressure;
}

float3 PressureForce(float densityA, float densityB, float nearDensityA, float nearDensityB, PTypeStruct PType_i, PTypeStruct PType_otherPIndex, float absDst, float avgInfluenceRadius, float3 normDst, int randSeed)
{
    float absPressureGradient = SmoothLiquidDer_optimised(absDst, avgInfluenceRadius);
    float absNearPressureGradient = SmoothLiquidNearDer_optimised(absDst, avgInfluenceRadius);

    float3 pressureGradient = 0;
    float3 nearPressureGradient = 0;

    if (absDst == 0) {
        // semirandom direction, not normalized
        float3 randVector = float3(randSeed % 0.28, randSeed % 0.18, randSeed % 0.3172);

        pressureGradient = 0.05 * randVector * absPressureGradient;
        nearPressureGradient = 0.05 * randVector * absNearPressureGradient;
    }
    else {
        pressureGradient = normDst * absPressureGradient;
        nearPressureGradient = normDst * absNearPressureGradient;
    }

    float avgPressure = SharedPressure(densityA, densityB, PType_i.TargetDensity, PType_otherPIndex.TargetDensity, PType_i.Pressure, PType_otherPIndex.Pressure);

    float avgNearPressure = SharedNearPressure(nearDensityA, nearDensityB, PType_i.NearPressure, PType_otherPIndex.NearPressure);

    if (densityA + densityB == 0) {return float3(0.0, 0.0, 0.0);}
    float3 pressureForce = (avgPressure * pressureGradient + avgNearPressure * nearPressureGradient) / avg(densityA, densityB);

    return pressureForce;
}

float3 ViscocityForce(float3 lastVelocityA, float3 lastVelocityB, PTypeStruct PType_i, PTypeStruct PType_otherPIndex, float viscocityLaplacian)
{
    float3 viscocityForce = viscocityLaplacian * (lastVelocityB - lastVelocityA);

    return viscocityForce * avg(PType_i.Viscosity, PType_otherPIndex.Viscosity);
}

float4 InteractionEffects(int pIndex)
{
    float mouseZ = Depth * 0.5;

    if (!LMousePressed && !RMousePressed) { return float4(0.0, 0.0, 0.0, 0.0); }
    int dir = 0;
    if (LMousePressed) {dir = -1;}
    else if (RMousePressed) {dir = 1;}

    float3 dst = PData[pIndex].PredPosition - float3(MouseX, MouseY, mouseZ);

    float absDst = length(dst);

    if (absDst > MaxInteractionRadius) {return float4(0.0, 0.0, 0.0, 0.0);}

    float absInteractionGradient = InteractionInfluence_optimised(absDst, MaxInteractionRadius);

    float TemperatureEffect = -dir * InteractionTemperaturePower;
    if (absDst == 0) {return float4(0.0, 0.0, 0.0, TemperatureEffect);}

    float3 interactionGradient = normalize(dst) * absInteractionGradient;

    float3 baseInteractionForce = interactionGradient * InteractionAttractionPower * dir;
    float fountainInteractionForce = 0;
    if (dir == -1)
    {
        float dstX = MaxInteractionRadius - abs(dst.x);
        fountainInteractionForce = InteractionFountainPower * absInteractionGradient * dstX;
    }

    float3 interactionForce = baseInteractionForce + float3(0.0, fountainInteractionForce, 0.0);

    return float4(interactionForce.x, interactionForce.y, interactionForce.z, TemperatureEffect);
}

float3 LiquidSpringForce(PTypeStruct PType_i, PTypeStruct PType_otherPIndex, int pIndex, int otherPIndex, int springIndex, float absDst, float avgInfluenceRadius, float3 normDst)
{
    SpringStruct spring = ParticleSpringsCombined[springIndex];

    // if spring is not already initialized, initialize a new one
    if (spring.PLinkedA == -1) { spring.PLinkedA = pIndex; spring.PLinkedB = otherPIndex; spring.RestLength = avgInfluenceRadius - 0.1; }


    // -- Modify spring and perform force calculations (plasticity, elasticity) --

    // Plasticity calculations
    float diffMng = absDst - spring.RestLength;
    float absDiffMng = abs(diffMng);
    int sgnDiffMng = sign(diffMng);
    float plasticity = avg(PType_i.SpringPlasticity, PType_otherPIndex.SpringPlasticity);
    float tolDeformation = avg(PType_i.SpringTolDeformation, PType_otherPIndex.SpringTolDeformation);

    // ∆L = ∆t * α * sign(r − L) * max(0,|r − L| − γL)
    spring.RestLength += LiquidSpringPlasticityModel(plasticity, sgnDiffMng, absDiffMng, tolDeformation, DeltaTime);

    // Elasticity calculations
    float stiffness = avg(PType_i.SpringStiffness, PType_otherPIndex.SpringStiffness);
    float absSpringForce = LiquidSpringForceModel(stiffness, spring.RestLength, avgInfluenceRadius, absDst);
    float3 springForce = absSpringForce * normDst;


    // -- Write new spring data --

    ParticleSpringsCombined[springIndex] = spring;


    // -- Return calculated force --

    return (isnan(springForce.x) || isnan(springForce.y) || isnan(springForce.z)) ? float3(0.0, 0.0, 0.0) : -springForce;
}

float TemperatureExchange(float viscocityLaplacian, float TemperatureA, float TemperatureB, float ThermalConductivityA, float ThermalConductivityB, float absDst, float DeltaTime)
{
    float temperatureDiff = TemperatureB - TemperatureA;
    float avgThermalConductivity = avg(ThermalConductivityA, ThermalConductivityB);

    return LiquidTemperatureExchangeModel(avgThermalConductivity, temperatureDiff, viscocityLaplacian, max(absDst, 0.1), DeltaTime);
}

[numthreads(TN_PS,1,1)]
void PreCalculations (uint3 id : SV_DispatchThreadID)
{
    int i = id.x;
    if (id.x >= (uint)ParticlesNum) { return; }

    PData[i].PredPosition = PData[i].Position + PData[i].Velocity * LookAheadFactor;

    // Make sure PredPositions[i] is within the world boundrary
    PData[i].PredPosition.x = clamp(PData[i].PredPosition.x, BorderPadding, Width - BorderPadding);
    PData[i].PredPosition.y = clamp(PData[i].PredPosition.y, BorderPadding, Height - BorderPadding);
    PData[i].PredPosition.z = clamp(PData[i].PredPosition.z, BorderPadding, Depth - BorderPadding);

    // LastVelocities
    PData[i].LastVelocity = PData[i].Velocity;
}

[numthreads(TN_PS,1,1)]
void CalculateDensities (uint3 id : SV_DispatchThreadID)
{
    if (id.x >= (uint)ParticlesNum) { return; }
    // Index from SpatialLookup to improve memory access coherency
    int i = SpatialLookup[id.x].x;

    // density
    float2 Dns = CalcDensity(i);
    PData[i].Density = Dns.x;
    PData[i].NearDensity = Dns.y;
}

[numthreads(TN_PS,1,1)]
void PrepSpringData (uint3 id : SV_DispatchThreadID)
{
    if (id.x >= (uint)ParticleSpringsCombinedHalfLength) { return; }

    int i = FrameBufferCycle
    ? id.x + ParticleSpringsCombinedHalfLength
    : id.x;

    ParticleSpringsCombined[i].PLinkedA = -1;
}

[numthreads(TN_PS,1,1)]
void TransferAllSpringData (uint3 id : SV_DispatchThreadID)
{
    if (id.x >= (uint)ParticleSpringsCombinedHalfLength) { return; }
    int i = FrameBufferCycle
    ? id.x
    : id.x + ParticleSpringsCombinedHalfLength;

    // B is connected to A
    SpringStruct lastSpring = ParticleSpringsCombined[i];
    if (lastSpring.PLinkedA == -1) { return; }
    PDataStruct PData_A = PData[lastSpring.PLinkedA];
    PDataStruct PData_B = PData[lastSpring.PLinkedB];

    float3 PosDiff = PData_A.Position - PData_B.Position;
    float absPosDiffSqr = dot(PosDiff, PosDiff);

    float avgInfluenceRadiusSqr = sqr(avg(PTypes[Extract_PType(PData_A.LastChunkKey_PType_POrder)].InfluenceRadius, PTypes[Extract_PType(PData_B.LastChunkKey_PType_POrder)].InfluenceRadius));

    // If A and B are in range of each other, transfer data. Otherwise, do not (spring data is deleted)
    if (absPosDiffSqr <= MaxInfluenceRadiusSqr && absPosDiffSqr <= avgInfluenceRadiusSqr)
    {
        int pOrder_A = Extract_POrder(PData_A.LastChunkKey_PType_POrder);
        int pOrder_B = Extract_POrder(PData_B.LastChunkKey_PType_POrder);
        int lastChunkKey_A = Extract_LastChunkKey(PData_A.LastChunkKey_PType_POrder);
        int lastChunkKey_B = Extract_LastChunkKey(PData_B.LastChunkKey_PType_POrder);
    
        int newChunkX_A = (uint)lastChunkKey_A % ChunksNum.x;
        int newChunkY_A = (int)((float)lastChunkKey_A / ChunksNum.x);
        int newChunkZ_A = (int)((float)lastChunkKey_A / ChunksNum.w);

        int newChunkX_B = (uint)lastChunkKey_B % ChunksNum.x;
        int newChunkY_B = (int)((float)lastChunkKey_B / ChunksNum.x);
        int newChunkZ_B = (int)((float)lastChunkKey_B / ChunksNum.w);

        // not optimal since the same calculations are performed by multiple threads (many springs in each chunk)
        int localSpringBCapacityOrder = 0;
        bool inRangeAB = false;
        bool shouldBreak = false;
        for (int x = -1; x <= 1 && !shouldBreak; x++)
        {
            int curChunkX = newChunkX_A + x;

            for (int y = -1; y <= 1 && !shouldBreak; y++)
            {
                int curChunkY = newChunkY_A + y;

                for (int z = -1; z <= 1 && !shouldBreak; z++)
                {
                    int curChunkZ = newChunkZ_A + z;

                    if (ValidChunk(curChunkX, curChunkY, curChunkZ))
                    {
                        if (curChunkX == newChunkX_B && curChunkY == newChunkY_B && curChunkZ == newChunkZ_B) { inRangeAB = true; shouldBreak = true; break; }
                        int curChunkKey = GetChunkKey(curChunkX, curChunkY, curChunkZ);
                        localSpringBCapacityOrder += SpringCapacities[curChunkKey].x;
                    }
                }
            }
        }
        if (inRangeAB)
        {
            int newNeighboorNum = localSpringBCapacityOrder + pOrder_B;

            int newChunkKey_A = GetChunkKey(newChunkX_A, newChunkY_A, newChunkZ_A);

            int nearSpringCapacity = (uint)SpringCapacities[newChunkKey_A].y / SpringCapacities[newChunkKey_A].x;
            int newSpringIndex = FrameBufferCycle
            ? SpringStartIndices_dbA[max(newChunkKey_A-1, 0)] + pOrder_A * nearSpringCapacity + newNeighboorNum + ParticleSpringsCombinedHalfLength
            : SpringStartIndices_dbB[max(newChunkKey_A-1, 0)] + pOrder_A * nearSpringCapacity + newNeighboorNum;

            ParticleSpringsCombined[newSpringIndex] = lastSpring;
        }
    }
}

[numthreads(TN_PS,1,1)]
void ParticleForces (uint3 id : SV_DispatchThreadID)
{
    if (id.x >= (uint)ParticlesNum) { return; }
    int i = SpatialLookup[id.x].x;

    PDataStruct PData_i = PData[i];
    PTypeStruct PType_i = PTypes[Extract_PType(PData_i.LastChunkKey_PType_POrder)];
    int pOrder = Extract_POrder(PData_i.LastChunkKey_PType_POrder);
    int lastChunkKey = Extract_LastChunkKey(PData_i.LastChunkKey_PType_POrder);
    float startTemperature = PData_i.Temperature;

    int chunkX = (uint)lastChunkKey % ChunksNum.x;
    int chunkY = (uint)((uint)lastChunkKey / ChunksNum.x) % ChunksNum.y;
    int chunkZ = (int)((uint)lastChunkKey / ChunksNum.w);
    int baseChunkKey = GetChunkKey(chunkX, chunkY, chunkZ);

    int nearSpringCapacity = (uint)SpringCapacities[baseChunkKey].y / SpringCapacities[baseChunkKey].x;

    float3 totPressureForce = float3(0.0, 0.0, 0.0);
    float3 totViscocityForce = float3(0.0, 0.0, 0.0);
    float3 totSpringForce = float3(0.0, 0.0, 0.0);
    float totTemperatureExchange = 0.0;
    int nNum = 0;
    for (int x = -1; x <= 1; x++)
    {
        int curChunkX = chunkX + x;

        for (int y = -1; y <= 1; y++)
        {
            int curChunkY = chunkY + y;

            for (int z = -1; z <= 1; z++)
            {
                int curChunkZ = chunkZ + z;

                if (!ValidChunk(curChunkX, curChunkY, curChunkZ)) { continue; }

                int chunkKey = GetChunkKey(curChunkX, curChunkY, curChunkZ);
                int startIndex = StartIndices[chunkKey];

                int Index = startIndex;
                while (Index < ParticlesNum && chunkKey == SpatialLookup[Index].y)
                {
                    int otherPIndex = SpatialLookup[Index].x;
                    if (i == otherPIndex) { Index += 1; nNum += 1; continue; }
                    PDataStruct PData_otherPIndex = PData[otherPIndex];
                    PTypeStruct PType_otherPIndex = PTypes[Extract_PType(PData_otherPIndex.LastChunkKey_PType_POrder)];
                    float otherPIndex_Mass = PType_otherPIndex.Mass;

                    float3 dst = PData_otherPIndex.Position - PData_i.Position;
                    float sqrDst = dot(dst, dst);
                    float avgInfluenceRadius = avg(PType_i.InfluenceRadius, PType_otherPIndex.InfluenceRadius);

                    if (sqrDst > MaxInfluenceRadiusSqr || sqrDst > sqr(avgInfluenceRadius)) { Index += 1; nNum += 1; continue; }
                    float absDst = sqrt(sqrDst);
                    float3 normDst = normalize(dst);

                    float viscocityLaplacian = SmoothViscosityLaplacian_optimised(absDst, avgInfluenceRadius);

                    int springIndex = FrameBufferCycle
                    ? SpringStartIndices_dbA[max(baseChunkKey-1, 0)] + pOrder * nearSpringCapacity + nNum + ParticleSpringsCombinedHalfLength
                    : SpringStartIndices_dbB[max(baseChunkKey-1, 0)] + pOrder * nearSpringCapacity + nNum;

                    totPressureForce += otherPIndex_Mass * PressureForce(PData_i.Density, PData_otherPIndex.Density, PData_i.NearDensity, PData_otherPIndex.NearDensity, PType_i, PType_otherPIndex, absDst, avgInfluenceRadius, normDst, otherPIndex);
                    totViscocityForce += otherPIndex_Mass * ViscocityForce(PData_i.LastVelocity, PData_otherPIndex.LastVelocity, PType_i, PType_otherPIndex, viscocityLaplacian);
                    if (PType_i.FluidSpringsGroup != 0 && PType_i.FluidSpringsGroup == PType_otherPIndex.FluidSpringsGroup)
                    {
                        totSpringForce += otherPIndex_Mass * LiquidSpringForce(PType_i, PType_otherPIndex, i, otherPIndex, springIndex, absDst, avgInfluenceRadius, normDst);
                    }

                    totTemperatureExchange += TemperatureExchange(viscocityLaplacian, PData_i.Temperature, PData_otherPIndex.Temperature, PType_i.ThermalConductivity, PType_otherPIndex.ThermalConductivity, absDst, DeltaTime);

                    // -- Increment Index each iteration - particle chunks algorithm, particle springs algorithm --

                    Index += 1;
                    nNum += 1;
                }
            }
        }
    }

    float4 interactionEffects = PType_i.Mass * InteractionEffects(i);

    float3 totForce = totPressureForce + totViscocityForce + totSpringForce + interactionEffects.xyz - float3(0.0, PType_i.Gravity, 0.0);
    PData[i].Velocity += totForce * DeltaTime / PType_i.Mass;
    PData[i].TemperatureExchangeBuffer = totTemperatureExchange + interactionEffects.w * DeltaTime;
}

[numthreads(TN_PS,1,1)]
void UpdatePositions (uint3 id : SV_DispatchThreadID)
{
    int i = id.x;

    PDataStruct PData_i = PData[i];
    int pTypeIndex = Extract_PType(PData_i.LastChunkKey_PType_POrder);
    PTypeStruct PType_i = PTypes[pTypeIndex];

    // Update particle position and velocity
    PData_i.Position += PData_i.Velocity * DeltaTime;

    // Validate particle position and velocity
    float2 validatedPosVelX = BoundraryCheck(PData_i.Position.x, PData_i.Velocity.x, PType_i.Damping, Width);
    float2 validatedPosVelY = BoundraryCheck(PData_i.Position.y, PData_i.Velocity.y, PType_i.Damping, Height);
    float2 validatedPosVelZ = BoundraryCheck(PData_i.Position.z, PData_i.Velocity.z, PType_i.Damping, Depth);

    PData[i].Position = float3(validatedPosVelX.x, validatedPosVelY.x, validatedPosVelZ.x);
    float3 newVelocity = float3(validatedPosVelX.y, validatedPosVelY.y, validatedPosVelZ.y);

    // Decrease velocity vector length with respect to the passive damping factor
    newVelocity *= max(length(newVelocity) - PType_i.PassiveDamping * DeltaTime, 0) / length(newVelocity);
    PData[i].Velocity = newVelocity;

    // Update particle temperature
    PData[i].Temperature += PData_i.TemperatureExchangeBuffer / PType_i.SpecificHeatCapacity;

    // State changes
    int stateKey = (uint)pTypeIndex % 3;
    switch(stateKey)
    {
        // Solid
        case 0:
            if (PData[i].Temperature > PType_i.FreezeThreshold + StateThresholdPadding)
            {
                Set_PType(PData[i].LastChunkKey_PType_POrder, (int)((uint)pTypeIndex / 3) + 1); // Solid -> Liquid
            }
            break;

        // Liquid
        case 1:
            if (PData[i].Temperature < PType_i.FreezeThreshold - StateThresholdPadding)
            {
                Set_PType(PData[i].LastChunkKey_PType_POrder, (int)((uint)pTypeIndex / 3) + 0); // Liquid -> Solid
            }
            else if (PData[i].Temperature > PType_i.VaporizeThreshold + StateThresholdPadding)
            {
                Set_PType(PData[i].LastChunkKey_PType_POrder, (int)((uint)pTypeIndex / 3) + 2); // Liquid -> Gas
            }
            break;

        // Gas
        case 2:
            if (PData[i].Temperature < PType_i.VaporizeThreshold - StateThresholdPadding)
            {
                Set_PType(PData[i].LastChunkKey_PType_POrder, (int)((uint)pTypeIndex / 3) + 1); // Gas -> Liquid
            }
            break;
    }
}